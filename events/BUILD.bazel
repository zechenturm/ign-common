load(
    "//ign_bazel:build_defs.bzl",
    "IGNITION_ROOT",
    "IGNITION_VISIBILITY",
    "generate_include_header",
    "ign_export_header",
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

public_headers_no_gen = [
    "include/ignition/common/Event.hh",
    "include/ignition/common/KeyEvent.hh",
    "include/ignition/common/MouseEvent.hh",
    "include/ignition/common/events/Types.hh",
]

sources = [
    "src/Event.cc",
    "src/KeyEvent.cc",
    "src/MouseEvent.cc",
]

test_sources = [
    "src/Event_TEST.cc",
    "src/KeyEvent_TEST.cc",
    "src/MouseEvent_TEST.cc",
]

ign_export_header(
    name = "include/ignition/common/events/Export.hh",
    export_base = "IGNITION_COMMON_EVENTS",
    lib_name = "ignition-common-events",
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "eventshh_genrule",
    out = "include/ignition/common/events.hh",
    hdrs = public_headers_no_gen + [
        "include/ignition/common/events/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/ignition/common/events/Export.hh",
    "include/ignition/common/events.hh",
]

cc_library(
    name = "events",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        IGNITION_ROOT + "ign_common",
    ],
)

cc_binary(
    name = "libignition-common4-events.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libignition-common4-events.so"],
    linkshared = True,
    deps = [":events"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":events",
        IGNITION_ROOT + "ign_common/test:test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]
